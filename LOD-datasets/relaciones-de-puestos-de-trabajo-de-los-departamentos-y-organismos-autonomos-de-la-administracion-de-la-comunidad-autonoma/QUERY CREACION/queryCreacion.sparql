PREFIX spif: <http://spinrdf.org/spif#>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX pproc:<http://contsem.unizar.es/def/sector-publico/pproc#>
PREFIX pc: <http://purl.org/procurement/public-contracts#>
PREFIX frapo:<http://purl.org/cerif/frapo/>
PREFIX dcterms:<http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
INSERT{
graph<http://euskadi.eus/graph/relaciones-de-puestos-de-trabajo-de-los-departamentos-y-organismos-autonomos-de-la-administracion-de-la-comunidad-autonoma>
{
    
    ?uriGeneral a frapo:EmploymentContract;
    rdfs:label ?labelElemento;
    dcterms:modified ?fechaModificacionParseada;
    ?uriFechaDescarga ?fechaDescargaParseada;  
    ?uriCodPuesto ?codPuesto;
    ?uriDotacion ?dotacion;
    dbpedia:occupation ?uriPuestoId;
    ?uriCodDpto ?codDepartamento;
    pproc:managingDepartment ?uriDepartamento;
    ?uriCodCentroOrganico ?codCentroOrganico;
    ?uriCentroOrganico ?uriCentroOrganicoId;
    ?uriCodCentroDestino ?uriCentroDestinoId;
    ?uriCategoriaRetributiva ?categoriaRetributiva;
    ?uriPerfilLinguistico ?perfilLinguistico;
    pproc:formalizedDate ?fechaPreceptividadParseada;
    pproc:ContractEconomicConditions ?importeRetributivo;
    }
}WHERE{
SERVICE<http://lod.eurohelp.es:7200/rdf-bridge/1803822894143>{
     ?numRow a <urn:Row>;
     <urn:col:FechaModificacion> ?fechaModificacion;
     <urn:col:FechaDescarga> ?fechaDescarga;
     <urn:col:CodPuesto> ?codPuesto;
     <urn:col:Dotacion> ?dotacion;
     <urn:col:Puesto> ?puesto;
     <urn:col:CodDepartamentoOrganismoAutonomo> ?codDepartamento;
     <urn:col:Departamento> ?departamento;
     <urn:col:DepartamentoConGuion> ?departamentoGuion;
     <urn:col:CodCentroOrganico> ?codCentroOrganico;
     <urn:col:CentroOrganico> ?centroOrganico;
     <urn:col:CodCentroDestino> ?codCentroDestino; 
     <urn:col:CentroDestinoSinGuion> ?codCentroDestinoSinGuion;
     <urn:col:CentroDestino> ?centroDestino;
     <urn:col:CategoriaRetributiva> ?categoriaRetributiva;
     <urn:col:PerfilLinguistico> ?perfilLinguistico;
     <urn:col:FechaDePreceptividad> ?fechaDePreceptividad;
     <urn:col:ImporteRetributivo> ?importeRetributivo;    
  }

    #Uri fecha
  bind(spif:parseDate(?fechaModificacion, "dd/MM/yyyy") as ?fechaModificacionParseada)
  bind(spif:parseDate(?fechaDescarga, "dd/MM/yyyy") as ?fechaDescargaParseada)
  bind(spif:parseDate(?fechaDePreceptividad, "dd/MM/yyyy") as ?fechaPreceptividadParseada)

  #Uri general
  bind(iri(concat("http://euskadi.eus/id/public-sector/administration/contract/", spif:encodeURL(?dotacion), "-", spif:encodeURL(?centroDestino),"-", spif:encodeURL(?codPuesto), "-", ?fechaModificacionParseada )) as ?uriGeneral)
  
  #Label 
  bind(STRLANG(concat(spif:encodeURL(?dotacion), "-", spif:encodeURL(?centroDestino),"-", spif:encodeURL(?codPuesto), "-", ?fechaModificacionParseada),"es") as ?labelElemento)

  #Uris predicados
  bind(iri("http://euskadi.eus/def/public-sector/administration/fecha-modificacion") as ?uriFechaModificacion)
  bind(iri("http://euskadi.eus/def/public-sector/administration/fecha-descarga") as ?uriFechaDescarga)
  bind(iri("http://euskadi.eus/def/public-sector/administration/cod-puesto") as ?uriCodPuesto)
  bind(iri("http://euskadi.eus/def/public-sector/administration/dotacion") as ?uriDotacion)
  bind(iri("http://euskadi.eus/def/public-sector/administration/cod-departamento") as ?uriCodDpto)
  bind(iri("http://euskadi.eus/def/public-sector/administration/cod-centro-organico") as ?uriCodCentroOrganico)
  bind(iri("http://euskadi.eus/def/public-sector/administration/centro-organico") as ?uriCentroOrganico)
  bind(iri("http://euskadi.eus/def/public-sector/administration/cod-centro-destino") as ?uriCodCentroDestino)  
  bind(iri("http://euskadi.eus/def/public-sector/administration/centro-destino") as ?uriCentroDestino)
  bind(iri("http://euskadi.eus/def/public-sector/administration/categoria-retributiva") as ?uriCategoriaRetributiva)
  bind(iri("http://euskadi.eus/def/public-sector/administration/perfil-linguistico") as ?uriPerfilLinguistico)
  bind(iri("http://euskadi.eus/def/public-sector/administration/importe-retributivo") as ?uriImporteRetributivo)

  #Etiquetas de idioma
  bind(STRLANG(?puesto,"es") as ?uriPuestoId)
  bind(STRLANG(?centroOrganico,"es") as ?uriCentroOrganicoId)
  bind(STRLANG(?codCentroDestinoSinGuion,"es") as ?uriCentroDestinoId)

  #Uri dpto
  bind(iri(concat("http://euskadi.eus/id/public-sector/administration/department/", spif:encodeURL(?departamentoGuion))) as ?uriDepartamento)

}