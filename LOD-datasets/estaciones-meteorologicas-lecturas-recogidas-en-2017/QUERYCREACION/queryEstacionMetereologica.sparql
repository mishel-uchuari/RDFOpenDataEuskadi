PREFIX estacion: <http://opendata.euskadi.eus/estacion/C040>
PREFIX medicion: <http://opendata.euskadi.eus/medicion/C040>
PREFIX spif: <http://spinrdf.org/spif#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#> 
PREFIX sdmx-attribute: <http://purl.org/linked-data/sdmx/2009/attribute#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>

INSERT{
  graph<http://es.euskadi.eus/graph/estaciones-meteorologicas-lecturas-recogidas-en-2017>{
  #Uniones iniciales 
  ?uriGeneral a qb:Observation;
  dct:date ?fechaParseada;
  geo:location ?nomEst;
  ?uriMedicion ?uridirMedia1100;
  ?uriMedicion ?urihumedad620;
  ?uriMedicion ?uriirradia200;
  ?uriMedicion ?uripresion150;
  ?uriMedicion ?urisigDir1100;
  ?uriMedicion ?urisigVel1100;
  ?uriMedicion ?uritemAire620;
  ?uriMedicion ?urivelMax1100;
  ?uriMedicion ?urivelMedia1100.
  
   #Parte dirMedia
   ?uridirMedia1100 a ?uridirMedia1100gen; 
   rdfs:label ?direccionMedia1100Label;
   rdfs:comment ?dirMediaComment; 
   sdmx-attribute:unitMeasure ?gradoscentigrados; 
   sdmx-measure:obsValue ?dirMedia1100.
   #Parte humedad620
   ?urihumedad620 a ?urihumedad620gen; 
   rdfs:comment ?humedadComment; 
   rdfs:label  ?humedad620Label;
   sdmx-attribute:unitMeasure ?porcentaje; 
   sdmx-measure:obsValue ?humedad620. 
   #Parte irradia200
   ?uriirradia200 a ?uriirradia200gen;
   rdfs:label  ?irradia200Label;
   rdfs:comment ?irradiaComment; 
   sdmx-attribute:unitMeasure ?watiosm2;
   sdmx-measure:obsValue ?irradia200. 
   #Parte presion150
   ?uripresion150 a ?uripresion150gen; 
   rdfs:comment ?presionComment; 
   rdfs:label ?presion150Label;
   sdmx-attribute:unitMeasure ?milibar; 
   sdmx-measure:obsValue ?presion150. 
   #Parte sigDir1100
   ?urisigDir1100 a ?urisigDir1100gen; 
   rdfs:comment ?sigDirComment; 
   rdfs:label  ?sigDir1100Label;
   sdmx-attribute:unitMeasure ?gradoscentigrados; 
   sdmx-measure:obsValue ?sigDir1100. 
   #Parte sigVel1100
   ?urisigVel1100 a ?urisigVel1100gen; 
   rdfs:comment ?sigVelComment; 
   rdfs:label ?sigVel1100Label; 
   sdmx-attribute:unitMeasure ?kilometrohora;
   sdmx-measure:obsValue ?sigVel1100. 
   #Parte temAire620
   ?uritemAire620 a ?uritemAire620gen; 
   rdfs:comment ?temAireComment; 
   rdfs:label ?temAire610Label;
   sdmx-attribute:unitMeasure ?gradoscentigrados;
   sdmx-measure:obsValue ?temAire620. 
   #Parte velMax1100
   ?urivelMax1100 a ?urivelMax1100gen; 
   rdfs:comment ?velMaxComment; 
   rdfs:label  ?velMax1100Label;
   sdmx-attribute:unitMeasure ?kilometrohora;   
   sdmx-measure:obsValue ?velMax1100. 
   #Parte velMedia1100
   ?urivelMedia1100 a ?urivelMedia1100gen; 
   rdfs:comment ?velMediaComment;
   rdfs:label ?velMedia1100Label;
   sdmx-attribute:unitMeasure ?kilometrohora; 
   sdmx-measure:obsValue ?velMedia1100.
  }
}WHERE{
   service <http://lod.eurohelp.es:7200/rdf-bridge/1658436154159> {
     ?numRow a <urn:Row> ;
     <urn:col:Fecha> ?date;
     <urn:col:HoraParseada> ?hora;
     <urn:col:FechaHora> ?dateTime;
     <urn:col:DirMed_a_1100cm> ?dirMedia1100;        
     <urn:col:Humedad_a_620cm> ?humedad620;
     <urn:col:Irradia_a_200cm> ?irradia200;        
     <urn:col:Presion_a_150cm> ?presion150;
     <urn:col:SigDir_a_1100cm> ?sigDir1100;        
     <urn:col:SigVel_a_1100cm> ?sigVel1100;
     <urn:col:TemAire_a_620cm> ?temAire620;        
     <urn:col:VelMax_a_1100> ?velMax1100;        
     <urn:col:VelMedia_a_1100cm> ?velMedia1100.
  }
bind(spif:parseDate(?date, "yyyy/MM/dd") as ?fechaParseada)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/direccionMedia1100-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora) )) as ?uridirMedia1100)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/humedad620-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora) )) as ?urihumedad620)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/irradia200-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora) )) as ?uriirradia200)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/presion150-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora) )) as ?uripresion150)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/sigDir1100-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora) )) as ?urisigDir1100)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/sigVel1100-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora))) as ?urisigVel1100)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/temAire610-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora))) as ?uritemAire620)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/velMax1100-" , spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora))) as ?urivelMax1100)
bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/element/velMedia1100-", spif:encodeURL(?fechaParseada), "-" , spif:encodeURL(?hora))) as ?urivelMedia1100)

  #Uris elementos (predicados) generales 
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/direccionMedia1100")) as ?uridirMedia1100gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/humedad620")) as ?urihumedad620gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/irradia200")) as ?uriirradia200gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/presion150")) as ?uripresion150gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/sigDir1100")) as ?urisigDir1100gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/sigVel1100")) as ?urisigVel1100gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/temAire610")) as ?uritemAire620gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/velMax1100")) as ?urivelMax1100gen)
bind(iri(concat("http://opendata.euskadi.eus/def/medio-ambiente/medicion/velMedia1100")) as ?urivelMedia1100gen)

   #Comentarios elementos  
bind(STRLANG("Dirección o procedencia media del viento expresada en grados","es") as ?dirMediaComment)
bind(STRLANG("Sigma de la dirección del viento expresada en grados","es") as ?sigDirComment)
bind(STRLANG("Humedad relativa del aire expresada en porcentaje","es") as ?humedadComment)
bind(STRLANG("Irradiancia global sobre una superficie plana expresada en watios por metro2","es") as ?irradiaComment)
bind(STRLANG("Presión atmosférica expresada en milibares","es") as ?presionComment)
bind(STRLANG("Sigma de la velocidad del viento expresada en kilómetros por hora","es") as ?sigVelComment)
bind(STRLANG("Temperatura del aire expresada en grados centígrados","es") as ?temAireComment)
bind(STRLANG("Racha máxima del viento horizontal expresada en kilómetros por hora","es") as ?velMaxComment)
bind(STRLANG("Velocidad media del viento expresada en kilómetros por hora","es") as ?velMediaComment)

  #Uri general
  bind(iri(concat("http://es.euskadi.eus/id/environment/air-quality/observation/C040-", spif:encodeURL(?fechaParseada) )) as ?uriGeneral)
  #Uri medicion
  bind(iri(concat("http://euskadi.eus/def/environment/air-quality/meassurement")) as ?uriMedicion)
  
  #Uris unidades de medida
bind(iri("http://ontology.fiesta-iot.eu/ontologyDocs/m3-lite.owl#DegreeCelsius") as ?gradoscentigrados)
bind(iri("http://dbpedia.org/ontology/percentage") as ?porcentaje)
bind(iri("http://purl.org/iot/vocab/m3-lite#WattPerMeterSquare") as ?watiosm2)
bind(iri("http://purl.org/iot/vocab/m3-lite#Millibar") as ?milibar)
bind(iri("http://purl.org/iot/vocab/m3-lite#KilometerPerHour") as ?kilometrohora)     
bind(iri("http://es.euskadi.eus/id/environment/air-quality/meassurement-station/C040") as ?nomEst)   

  #Labels
  bind(STRLANG(concat("direccionMedia1100-", ?fechaParseada),"es") as ?direccionMedia1100Label)
  bind(STRLANG(concat("humedad620-", ?fechaParseada),"es") as ?humedad620Label)
  bind(STRLANG(concat("irradia200-", ?fechaParseada),"es") as ?irradia200Label)
  bind(STRLANG(concat("presion150-", ?fechaParseada),"es") as ?presion150Label)
  bind(STRLANG(concat("sigDir1100-", ?fechaParseada),"es") as ?sigDir1100Label)
  bind(STRLANG(concat("sigVel1100-", ?fechaParseada),"es") as ?sigVel1100Label)
  bind(STRLANG(concat("temAire610-", ?fechaParseada),"es") as ?temAire610Label)
  bind(STRLANG(concat("velMax1100-", ?fechaParseada),"es") as ?velMax1100Label)
  bind(STRLANG(concat("velMedia1100-", ?fechaParseada),"es") as ?velMedia1100Label)
    }